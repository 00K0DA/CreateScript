Echo off

Rem ************************************************************************
Rem * バッチ名：Evernote検索バッチ
Rem * 処理内容：検索用の文字列を入力させ、その入力値に一致するEvernoteを検索
Rem *           する。検索されるEvernoteはノート内に記述されている内容で検索
Rem *           されます
Rem ************************************************************************

Rem --------------------------- 設定 --------------------------- 
Rem コマンドラインで検索できるEXEまでのパス
Set ENScriptPath=%ProgramFiles(x86)%\Evernote\Evernote\ENScript.exe

Rem ---------------------- メイン処理部分 ---------------------- 
Rem タイトル表示処理
Call :DisplayTitle

Rem Evernotenの検索処理の実行
Call :RunSearchEvernotes

Rem 終了処理
Call :EndProcess

Rem ---------------------- ラベル処理部分 ---------------------- 

Rem ***************************************************************
Rem * 処 理 名：タイトルの表示
Rem * 引    数：なし
Rem * 処理内容：タイトルを表示し処理内容の概要を画面に出力します
Rem ***************************************************************
:DisplayTitle

    Call :DisplayMessage " ************************************************************************ " 1 0
    Call :DisplayMessage " * バッチ名：Evernote検索バッチ                                           " 0 0
    Call :DisplayMessage " * 処理内容：検索用の文字列を入力させ、その入力値に一致するEvernoteを検索 " 0 0
    Call :DisplayMessage " *           する。検索されるEvernoteはノート内に記述されている内容で検索 " 0 0
    Call :DisplayMessage " *           されます                                                     " 0 0
    Call :DisplayMessage " ************************************************************************ " 0 1
    
    Exit /b

Rem ***************************************************************
Rem * 処 理 名：Evernoteの検索処理の実行
Rem * 引    数：なし
Rem * 処理内容：検索文字列をユーザーに対話で入力させ、その内容で
Rem *           Evernoteを検索させる
Rem ***************************************************************
:RunSearchEvernotes

    Rem ユーザーにEvernoteで検索させるための文字列を入力させる
    Set /p SearchString= "検索する文字列の入力　＞　"

    Rem Evernoteの検索実行し、検索結果を表示
    "%ENScriptPath%" showNotes /q "%SearchString%"

    Exit /b

Rem ***************************************************************
Rem * 処 理 名：メッセージの表示
Rem * 引    数：1 表示させるメッセージ
Rem *           2 表示させるメッセージ前に改行を含めるかどうか（1は含める、それ以外は含めない）
Rem *           3 表示させるメッセージ後に改行を含めるかどうか（1は含める、それ以外は含めない）
Rem * 処理内容：表示させるメッセージの前後に改行を含めて表示するか
Rem *           どうかを引数に応じて行う
Rem *           使用方法
Rem *             Call :DisplayMessage "ああああ aaaaa bbbbb" 1 1
Rem *             ※引数は必ず3つ渡すこと
Rem *               表示するメッセージは必ずダブルクォーテーションで囲むこと
Rem ***************************************************************
:DisplayMessage

    Rem メッセージ前に改行を含める
    If %~2 Equ 1 (
        Call :DisplayNewLine
    ) 

    Rem ダウブルクォーテーションを削除して表示
    Echo %~1

    Rem メッセージ後に改行を含める
    If %~3 Equ 1 (
        Call :DisplayNewLine
    )
    
    Exit /b

Rem ***************************************************************
Rem * 処 理 名：改行メッセージの表示
Rem * 引    数：なし
Rem * 処理内容：コマンドプロンプトに改行を表示させる
Rem ***************************************************************
:DisplayNewLine

    Rem 改行を表示
    Echo;
    
    Exit /b

Rem ***************************************************************
Rem * 処 理 名：終了処理
Rem * 引    数：なし
Rem * 処理内容：バッチ処理を終了させる
Rem ***************************************************************
:EndProcess

    Rem 処理の終了
    Exit